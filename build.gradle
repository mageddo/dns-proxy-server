buildscript {
  repositories {
    mavenLocal()
    mavenCentral()
    gradlePluginPortal()
  }
}

plugins {
  id "java"
  id 'io.quarkus'
  id 'net.researchgate.release' version '3.0.2'
}

repositories {
  mavenLocal()
  mavenCentral()
  maven { url "https://oss.sonatype.org/service/local/repositories/releases/content" }
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

configurations.all {
  resolutionStrategy {
    force 'net.java.dev.jna:jna:5.13.0'
    force 'net.java.dev.jna:jna-platform:5.13.0'
    force 'org.mockito:mockito-core:5.1.1'
  }
}

dependencies {

  annotationProcessor("com.google.dagger:dagger-compiler:2.45")
  implementation("com.google.dagger:dagger:2.45")

  testAnnotationProcessor("com.google.dagger:dagger-compiler:2.45")
  testImplementation("com.google.dagger:dagger:2.45")

  compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.+'
  annotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.+'

  implementation enforcedPlatform("${quarkusPlatformGroupId}:${quarkusPlatformArtifactId}:${quarkusPlatformVersion}")

  compileOnly group: 'com.mageddo.nativeimage', name: 'reflection-config-generator', version: '2.4.3'
  annotationProcessor group: 'com.mageddo.nativeimage', name: 'reflection-config-generator', version: '2.4.3'

  implementation 'io.quarkus:quarkus-arc'
  implementation 'io.quarkus:quarkus-resteasy'
  implementation 'io.quarkus:quarkus-resteasy-jsonb'

  implementation group: 'net.java.dev.jna', name: 'jna-platform', version: '5.13.0'

  implementation group: 'dnsjava', name: 'dnsjava', version: '3.5.2'

  implementation group: 'com.github.docker-java', name: 'docker-java-core', version: '3.2.14'
  implementation group: 'com.github.docker-java', name: 'docker-java-transport-httpclient5', version: '3.2.14'

  implementation group: 'info.picocli', name: 'picocli', version: '4.7.1'

  implementation 'com.mageddo.commons:commons-lang:0.1.6'
  implementation 'org.apache.commons:commons-exec:1.3'

  testCompileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.+'
  testAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.+'
  testImplementation 'io.quarkus:quarkus-junit5'
  testImplementation("io.quarkus:quarkus-junit5-mockito")
  testImplementation 'io.rest-assured:rest-assured'
  testImplementation(group: "org.mockito", name: "mockito-junit-jupiter", version: "5.1.+")

}

test {
  exclude "**/*CompTest.class"
  useJUnitPlatform()
  testLogging {
    events "passed", "skipped", "failed"
  }
}

task compTest(type: Test) {
  include "**/*CompTest.class"
  failFast = true
  testLogging {
    events "passed", "skipped", "failed"
  }
}

compileJava {
  options.encoding = 'UTF-8'
  options.compilerArgs << '-parameters'
}

compileTestJava {
  options.encoding = 'UTF-8'
}

processResources {
  filesMatching("**/application.properties") {
    expand version: java.util.Optional
      .ofNullable(System.getenv("BUILD_NUMBER"))
      .orElse(String.valueOf(project.version))
  }
}

release {
  project.ext.set("release.useAutomaticVersion", true)
  git {
    requireBranch.set("")
  }
  failOnCommitNeeded = true
  failOnPublishNeeded = true
  failOnUnversionedFiles = true
  buildTasks = []
}

// don't create tags as github-cli will do that
preTagCommit.enabled = false
createReleaseTag.enabled = false

confirmReleaseVersion {
  doLast {
    // codigo com a versao atual...
  }
}

tasks.register("updateNewVersion") {
  doLast {
    updateVersion("README.md", "/\\d+\\.\\d+\\.\\d+/", "/${version}/")
    updateVersion("README.md", "\\d+\\.\\d+\\.\\d+\\.tgz", "${version}.tgz")
  }
}
tasks["updateVersion"].finalizedBy("updateNewVersion")


def updateVersion(fileName, pattern, version) {
  println("> file=${fileName}, pattern=${pattern}, to=${version}")
  def f = file("${rootDir}/$fileName")
  def text = f.text
  f.withWriter { w ->
    w << text.replaceAll(pattern, version)
  }
}
